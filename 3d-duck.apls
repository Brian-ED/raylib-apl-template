#!/usr/bin/dyalogscript
dir ← ⊃1⎕NPARTS''
rl ← 0⎕Fix dir,'../raylib-apl/link/raylib.apln'
rl.Init (dir,'../raylib-apl/link/')⍬

rl.InitWindow 800 800 'Hello!!!'
rl.DisableCursor

⍝ Define the camera to look into our 3d world
camera  ←⊂ 50 50 50 ⍝ Camera position
camera ,←⊂  0 10  0 ⍝ Camera looking at point
camera ,←⊂  0  1  0 ⍝ Camera up vector (rotation towards target)
camera ,←⊂ 45       ⍝ Camera field-of-view Y
camera ,←⊂ rl.CameraProjection.CAMERA_PERSPECTIVE ⍝ Camera type
camera ,←  4⍴0

model _ ← rl.LoadModelRetPtr ⍬ (dir,'resources/RubberDuck_LOD0.obj')

⍝ For every frame till user closes the window
:While ~rl.WindowShouldClose

    ⍝ Look for keystrokes from user, move the camera
    camera ← rl.UpdateCamera ((,⊂camera) rl.CameraMode.CAMERA_THIRD_PERSON)

    ⍝ Preparing another buffer to draw the new frame, with white background
    rl.BeginDrawing
        rl.ClearBackground ⊂8↑rl.color.white

        ⍝ Draws FPS at x=40 y=40
        rl.DrawFPS 40 40

        ⍝ Draw 3d stuff relative to the camera
        rl.BeginMode3D ⊂camera

            ⍝ draws grid
            rl.DrawGrid 20 10

            ⍝ Draw the duck model at position
            position ← 0 0 0  ⋄  scale ← 1
            rl.DrawModel (model position scale (8↑rl.color.white))

        ⍝ Stop drawing 3d
        rl.EndMode3D

    ⍝ wait till next frame is needed, then switch to this new buffer we've drawn
    rl.EndDrawing
:EndWhile

⍝ Unloading model, freeing it's memory.
rl.UnloadModel ,⊂model
rl.CloseWindow
